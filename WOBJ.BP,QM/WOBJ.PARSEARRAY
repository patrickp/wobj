SUBROUTINE WOBJ.PARSEARRAY(JSTR,INDEXPOS,PARSEARRAY)
**********************************************************************
*
* Copyright (C) 2017 Zumasys, Inc., All Rights Reserved
*
* Written by: Patrick Payne, Zumasys
* Date: December 2017
* Description: parse json array
*
**********************************************************************
*
INCLUDE WOBJ.INCLUDE
*
PARSEVARRAY=""
IF WOBJ.DEBUG THEN PRINT "PARSEARRAY - STARTING"
*
CALL WOBJ.SKIPCHAR(JSTR,INDEXPOS)
*
IF JSTR[INDEXPOS,1] <> "[" THEN
    PSERRORS<-1>="Invalid Array at position ":INDEXPOS:" : ":JSTR[INDEXPOS,20]
    RETURN
END
*
LEN.JSTR = LEN(JSTR)
INDEXPOS+=1
*
JSONOBJECT(JSONOBJECT$NODECNTR)+=1
NODEID=JSONOBJECT(JSONOBJECT$NODECNTR)
JSONOBJECT(JSONOBJECT$NODETYPE)<1,NODEID>="A"; *SET AS OBJECT
*
LOOP
    CALL WOBJ.SKIPCHAR(JSTR,INDEXPOS)
    C=JSTR[INDEXPOS,1]
    IF WOBJ.DEBUG THEN PRINT "PARSEARRAY: ":NODEID:" - LOOKING AT ":C
    BEGIN CASE
        CASE C = "]"
            IF WOBJ.DEBUG THEN PRINT "PARSEARRAY: ":NODEID:" EXIT ON ]"
            INDEXPOS+=1
            EXIT
        CASE C = ","
            INDEXPOS+=1
            CALL WOBJ.SKIPCHAR(JSTR,INDEXPOS)
        CASE INDEXPOS > LEN.JSTR
            PSERRORS<-1>="Missing ']': ":JSTR[LEN(JSTR)-20,20]
            RETURN
    END CASE
    *
    CALL WOBJ.PARSEVALUE(JSTR,INDEXPOS,PARSEVALUE)
    *
    JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEID>+=1
    PARTPOS=JSONOBJECT(JSONOBJECT$NODELENGTH)<1,NODEID>
    BEGIN CASE
        CASE PARSEVALUE=""
            PSERRORS<-1>="Invalid value syntax for array element ":(PARTPOS-1)
            IF WOBJ.DEBUG THEN PRINT "PARSEARRAY ":NODEID:" invalid value syntax"
            RETURN
        CASE PARSEVALUE<1>="A" OR PARSEVALUE<1>="O"
            * ARRAY OR OBJECT
            JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEID,PARTPOS>=PARSEVALUE<1>
            JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,NODEID,PARTPOS>=PARSEVALUE<2>
        CASE 1
            JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEID,PARTPOS>=PARSEVALUE<1>
            V=PARSEVALUE<2>
            IF INDEX(V,LF,1) OR INDEX(V,CR,1) THEN
                V=OCONV(V,"MX0C");*QM*
                E="H"
            END ELSE
                E="A"
            END
            JSONOBJECT(JSONOBJECT$NODEPARTS.VALUE)<1,NODEID,PARTPOS>=V
            JSONOBJECT(JSONOBJECT$NODEPARTS.ENCODING)<1,NODEID,PARTPOS>=E
    END CASE
    IF WOBJ.DEBUG THEN PRINT "PARSEARRAY ":NODEID:" ":PARSEVALUE<1>:" ":PARSEVALUE<2>
REPEAT
PARSEARRAY="A"
PARSEARRAY<2>=NODEID
IF WOBJ.DEBUG THEN PRINT "PARSEARRAY - EXITING"
RETURN
END

